buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.1'
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

group 'habanero'
version '0.1'
mainClassName = "io.ktor.server.netty.EngineMain"

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://kotlin.bintray.com/ktor' }
    maven { url "https://dl.bintray.com/konform-kt/konform" }
    maven { url 'https://jitpack.io' }
}

dependencies {
    // Kotlin (Language)
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Ktor (Web framework)
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-locations:$ktor_version"
    implementation "io.ktor:ktor-auth:$ktor_version"
    implementation "io.ktor:ktor-auth-jwt:$ktor_version"
    implementation "io.ktor:ktor-gson:$ktor_version"

    // Logback (Logger)
    implementation "ch.qos.logback:logback-classic:$logback_version"

    // Konform (Validation)
    implementation 'io.konform:konform:0.1.0'

    // KMongo (MongoDB client)
    implementation 'org.litote.kmongo:kmongo:3.12.0'
    implementation 'org.litote.kmongo:kmongo-coroutine:3.12.0'

    // Koin (Dependency Injection)
    implementation 'org.koin:koin-ktor:2.0.0-GA6'
    implementation 'org.koin:koin-core-ext:2.0.0-GA6'

    // ULID
    implementation 'com.github.guepardoapps:kulid:1.1.2.0'

    // Spring Security
    implementation 'org.springframework.security:spring-security-core:5.2.1.RELEASE'

    // java-jwt (JSON Web Token)
    implementation 'com.auth0:java-jwt:3.9.0'

    // Java Mail (Mail Client)
    implementation 'com.sun.mail:javax.mail:1.6.2'

    // Test
    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
    testImplementation "org.koin:koin-test:2.0.0-GA6"
    testImplementation "com.github.kirviq:dumbster:1.7.1"
}

shadowJar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}
